find_package(Boost REQUIRED)
find_package(Qt5Quick 5.3.0 CONFIG REQUIRED)
include_directories(${Boost_INCLUDE_DIR})

set(QT5_REQUIRED_COMPILER_FLAGS "")
foreach(_flag ${Qt5Quick_COMPILE_DEFINITIONS})
    set(QT5_REQUIRED_COMPILER_FLAGS "${QT5_REQUIRED_COMPILER_FLAGS} \"-D${_flag}\", ")
endforeach()
foreach(_flag ${Qt5Quick_INCLUDE_DIRS})
    set(QT5_REQUIRED_COMPILER_FLAGS "${QT5_REQUIRED_COMPILER_FLAGS} \"-isystem\", \"${_flag}\",")
endforeach()
foreach(_flag ${Qt5Quick_EXECUTABLE_COMPILE_FLAGS})
    set(QT5_REQUIRED_COMPILER_FLAGS "${QT5_REQUIRED_COMPILER_FLAGS} \"${_flag}\",")
endforeach()
configure_file(config-tests.h.cmake ${CMAKE_CURRENT_BINARY_DIR}/config-tests.h @ONLY)

add_library(testCommonObjs OBJECT testCommon.cpp) #only compile once, not for every test

function(converter_unit_test _name)
  add_executable(test-${_name} test${_name}.cpp $<TARGET_OBJECTS:testCommonObjs> $<TARGET_OBJECTS:converterObjs>)
  target_link_libraries(test-${_name} ${REQUIRED_CLANG_LIBS} ${REQUIRED_LLVM_LIBS})
  add_test(NAME ${_name}-cxx COMMAND test-${_name})
endfunction()

converter_unit_test(Simple)
converter_unit_test(Overloads)
converter_unit_test(Scoping)
converter_unit_test(Membercall)
converter_unit_test(Disconnect)
converter_unit_test(MissingMocInclude)
converter_unit_test(NullptrCustomization)
converter_unit_test(SmartPointer)
converter_unit_test(QQuickItem)
converter_unit_test(UsingDirectives)

set(CMAKE_AUTOMOC ON)

function(build_old_and_new _name)
	add_executable(${_name}-qt4style input/${_name}.cpp)
	target_link_libraries(${_name}-qt4style Qt5::Core)
	add_executable(${_name}-qt5style refactored/${_name}.cpp)
	target_link_libraries(${_name}-qt5style Qt5::Core)
endfunction()

build_old_and_new(simple)
build_old_and_new(membercall)
build_old_and_new(scoping)
build_old_and_new(overloads)
build_old_and_new(disconnect)
build_old_and_new(smart_pointer)
build_old_and_new(qquickitem)
target_link_libraries(qquickitem-qt4style Qt5::Quick)
target_link_libraries(qquickitem-qt5style Qt5::Quick)
build_old_and_new(using_directives)
# This one does not work, there is no signal = nullptr overload for QObject::disconnect
